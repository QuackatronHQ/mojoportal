SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

/********************************************************************
start mp_Users_SelectPage
********************************************************************/
ALTER PROCEDURE [dbo].[mp_Users_SelectPage]
@PageNumber INT,
@PageSize INT,
@BeginsWith NVARCHAR(50),
@SiteID INT,
@SortMode INT,
@NameFilterMode NVARCHAR(10)
AS
DECLARE @PageLowerBound INT
DECLARE @PageUpperBound INT

SET @PageLowerBound = (@PageSize * @PageNumber) - @PageSize
SET @PageUpperBound = @PageLowerBound + @PageSize + 1

CREATE TABLE #PageIndexForUsers (
IndexID INT IDENTITY(1, 1) NOT NULL,
UserID INT
)

IF @BeginsWith IS NULL
OR @BeginsWith = ''
BEGIN
INSERT INTO #PageIndexForUsers (UserID)
SELECT UserID
FROM mp_Users
WHERE ProfileApproved = 1
AND DisplayInMemberList = 1
AND SiteID = @SiteID
AND IsDeleted = 0
ORDER BY 
(CASE @SortMode WHEN 1 THEN [LastName] END) ASC,
(CASE @SortMode WHEN 1 THEN [FirstName] END) ASC,
(CASE @SortMode WHEN 2 THEN [LastName] END) DESC,
(CASE @SortMode WHEN 2 THEN [FirstName] END) DESC,
(CASE @SortMode WHEN 3 THEN [DateCreated] END) ASC,
(CASE @SortMode WHEN 4 THEN [DateCreated] END) DESC,
(CASE @SortMode WHEN 5 THEN [Name] END) ASC,
(CASE @SortMode WHEN 6 THEN [Name] END) DESC
END
ELSE
BEGIN
INSERT INTO #PageIndexForUsers (UserID)
SELECT UserID
FROM mp_Users
WHERE ProfileApproved = 1
AND DisplayInMemberList = 1
AND SiteID = @SiteID
AND IsDeleted = 0
AND (
(@NameFilterMode = 'display' AND LOWER([Name]) LIKE LOWER(@BeginsWith) + '%')
OR ((@NameFilterMode = 'lastname' AND LOWER([LastName]) LIKE LOWER(@BeginsWith) + '%') OR (@NameFilterMode = 'lastname' AND LOWER([Name]) LIKE LOWER(@BeginsWith) + '%'))
OR (@NameFilterMode <> 'display' AND @NameFilterMode <> 'lastname' AND LOWER([Name]) LIKE LOWER(@BeginsWith) + '%')
)
ORDER BY
(CASE @SortMode WHEN 1 THEN [LastName] END) ASC,
(CASE @SortMode WHEN 1 THEN [FirstName] END) ASC,
(CASE @SortMode WHEN 2 THEN [LastName] END) DESC,
(CASE @SortMode WHEN 2 THEN [FirstName] END) DESC,
(CASE @SortMode WHEN 3 THEN [DateCreated] END) ASC,
(CASE @SortMode WHEN 4 THEN [DateCreated] END) DESC,
(CASE @SortMode WHEN 5 THEN [Name] END) ASC,
(CASE @SortMode WHEN 6 THEN [Name] END) DESC
END

SELECT *
FROM mp_Users u
JOIN #PageIndexForUsers p ON u.UserID = p.UserID
WHERE u.ProfileApproved = 1
AND u.SiteID = @SiteID
AND u.IsDeleted = 0
AND p.IndexID > @PageLowerBound
AND p.IndexID < @PageUpperBound
ORDER BY p.IndexID

DROP TABLE #PageIndexForUsers

/********************************************************************
END mp_Users_SelectPage
********************************************************************/